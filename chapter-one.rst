AWK入门指南
========================

Awk是一种方便且表达能力强的程序设计语言，可应用于各种计算和数据处理任务。本章是个入门指南，让你能够尽快地开始编写你自己的程序。第二章将描述整个语言，而剩下的章节将向你展示如何使用Awk来解决许多不同方面的问题。纵观全书，我们尽量选择了一些对你有用、有趣并且有指导意义的实例。

1.1 起步
--------------

有用的awk程序往往很简短，仅仅一两行。假设你有一个名为emp.data的文件，其中包含员工的姓名、薪资（美元/小时）以及小时数，一个员工一行数据，如下所示：

======== ======== ========
Beth      4.00     0
Dan       3.75     0
kathy     4.00     10
Mark      5.00     20
Mary      5.50     22
Susie     4.25     18
======== ======== ========

现在你想打印出工作时间超过零小时的员工的姓名和工资（薪资乘以时间）。这种任务对于awk来说就是小菜一碟。输入这个命令行就可以了：::

    awk '$3 >0 { print $1, $2 * $3 }' emp.data

你应该会得到如下输出：

| Kathy 40
| Mark 100
| Mary 121   
| Susie 76.5


该命令行告诉系统执行引号内的awk程序，从输入文件emp.data获取程序所需的数据。引号内的部分是个完整的awk程序，包含单个模式-行为语句。模式$3>0用于匹配第三列大于0的输入行，行为::

    { print $1, $2 * $3 }

打印每个匹配行的第一列以及第二第三列的乘积。

如果你想打印出还没工作过的员工的姓名，则输入命令行：::

    awk '$3 == 0 { print $1 }' emp.data

这里，模式$3 == 0 匹配第三列等于0的行，行为::

    { print $1 }

打印该行的第一列。

当你阅读本书时，应该尝试执行与修改示例程序。大多数程序都很简短，所以你能快速理解awk是如何工作的。在Unix系统上，以上两个事务在终端里看起来是这样的：
    
| $ awk '$3 > 0 { print $1, $2 * $3 }' emp.data  
| Kathy 40  
| Mark 100  
| Mary 121  
| Susie 76.5  
| $ awk '$3 == 0 { print $1 }' emp.data  
| Beth  
| Dan  
| $

行首的$是系统提示符，也许在你的机器上不一样。

**AWK程序的结构**

让我们回头看一下到底发生了什么事情。上述的命令行中，引号之间的部分是awk编程语言写就的程序。本章中的每个awk程序都是一个或多个模式-行为语句的序列：

| pattern   { action }
| pattern   { action }
| ...

awk的基本操作是一行一行地扫描输入，搜索匹配任意程序中模式的行。词语“匹配”的准确意义是视具体的模式而言，对于模式$3 >0 来说，意思是“条件为真”。

每个模式依次测试每个输入行。对于匹配到行的模式，其对应的行为（也许包含多步）得到执行，然后读取下一行并继续匹配，直到所有的输入读取完毕。

上面的程序都是模式与行为的典型示例。::

    $3 == 0 { print $1 }

是单个模式-行为语句；对于第三列为0的每行，打印其第一行。

模式-行为语句中的模式或行为（但不是同时两者）都可以省略。如果某个模式没有行为，例如：::

    $3 == 0

那么模式匹配到的每一行（即，对于该行，条件为真）都会被打印出来。该程序会打印emp.data文件中第三列为0的两行

| Beth  4.00    0
| Dan   3.75    0

如果有个没有模式的行为，例如：::

    { print $1 }

那么这种情况下的行为会打印每个输入行的第一列。

由于模式和行为两者任一都是可选的，所以需要使用括弧包着行为以区分于其他模式。

**执行AWK程序**

.. toctree::
    :hidden:

